<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" width="70" height="50" xmlns:local="*" buttonMode="true">
	
	<fx:Declarations>
		<fx:String id="xpos" />
		<fx:String id="ypos" />
		<fx:String id="sourceType" />
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import content.workarea.WorkArea;
			
			import listeners.MaterialToPreviewEvent;
			import listeners.ObjectDropEvent;
			import listeners.SelectMaterialEvent;
			
			import mx.events.FlexEvent;
			
			private var _previewType:String;
			private var _previewSource:String;
			private var _fullSizeSource:String;
			
			private var _isPortfolio:String;
			private var _isFinalPortfolio:String;
			
			public var canBeAddedToPF:Boolean = true;
			
 			private var _groupColour:uint = uint(mx.core.FlexGlobals.topLevelApplication.currentGroup.colour);
			
			[Bindable] public var colorWeigth:Number = 3;
			[Bindable] private var materialPreviewSource:String = "";
				
			[Bindable]
			public function get groupColour():uint
			{
				return _groupColour;
			}

			public function set groupColour(value:uint):void
			{
				_groupColour = value;
			}

			public function get isFinalPortfolio():String
			{
				return _isFinalPortfolio;
			}

			public function set isFinalPortfolio(value:String):void
			{
				_isFinalPortfolio = value;
				
				if(sourceType == WorkArea.PORTFOLIO_TYPE) {
					if(value == "true" && canBeAddedToPF) {
						deleter.visible = true;
					}
					else {
						deleter.visible = false;
					}
				}
			}

			public function get isPortfolio():String
			{
				return _isPortfolio;
			}
			
			public function set isPortfolio(value:String):void {
				_isPortfolio = value;
				
				if(sourceType == WorkArea.ASSET_TYPE) {
					if(value == "true" && canBeAddedToPF) {
						deleter.visible = true;
					}
					else {
						deleter.visible = false;
					}
				}	
			}

			public function get fullSizeSource():String
			{
				return _fullSizeSource;
			}

			public function get previewSource():String
			{
				return _previewSource;
			}

			public function get previewType():String
			{
				return _previewType;
			}

			/**
			 * 
			 * Called from the instantiator to set up preview type and source
			 * 
			 **/
			public function setupPreview(previewType:String, fullSizeSource:String, previewSource:String):void {
				_previewType = previewType;
				_fullSizeSource = fullSizeSource;
				_previewSource = previewSource;
				
				switch(previewType) {
					case WorkArea.IMAGE_TYPE:
						sourceImage.source = previewSource.replace("http://localhost:4567/", "");
						sourceImage.visible = true;
						materialPreviewSource = "assets/15w.png";
						break;
					case WorkArea.VIDEO_TYPE :
						sourceImage.source = previewSource;
						sourceImage.visible = true;
						materialPreviewSource = "assets/16w.png";
						break;
				}
			}
				
			protected function onSelected(event:FlexEvent):void {
				var mtpe:MaterialToPreviewEvent = new MaterialToPreviewEvent(resourceManager.getString('resources', 'MATERIAL_TO_PREVIEW'), true);
				mtpe.materialItem = this;
				dispatchEvent(mtpe);
			}
			
			protected function deleteItemFromPortfolio(event:MouseEvent):void {
				var sme:SelectMaterialEvent = new SelectMaterialEvent(resourceManager.getString('resources', 'ADD_MATERIAL_TO_PORTFOLIO'), true);
				if(sourceType == WorkArea.ASSET_TYPE) {
					isPortfolio = "false";
				}
				else if(sourceType == WorkArea.PORTFOLIO_TYPE) {
					isFinalPortfolio = "false";
				}
				sme.materialItem = this;
				dispatchEvent(sme);
			}
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="normal" />
		<s:State name="hovered"  />
		<s:State name="selected" enterState="onSelected(event)"/>
	</s:states>
	
	<s:Group width="70" height="50">
		<s:Rect left="0" right="0" top="0" bottom="0">
			<s:fill>
				<s:SolidColor color="0x000000" />
			</s:fill>
			<s:stroke>
				<s:SolidColorStroke color="{groupColour}" weight="{colorWeigth}"/>
			</s:stroke>
		</s:Rect>

		<s:Image id="sourceImage" visible="false" horizontalCenter="0" verticalCenter="0" top="3" right="3" bottom="3" left="3" scaleMode="letterbox" />
		
		<s:Group id="clickableGroup" width="100%" height="100%" click="currentState='selected';">
			<s:Rect left="3" right="3" top="3" bottom="3">
				<s:fill>
					<s:SolidColor color="0x000000" alpha=".5" alpha.hovered="0" alpha.selected="0" />
				</s:fill>
			</s:Rect>
			<s:Image source="{materialPreviewSource}" horizontalCenter="0" verticalCenter="0" excludeFrom="hovered, selected" enabled="false" />
		</s:Group>
	</s:Group>

	<s:Image id="deleter" source="@Embed('assets/close.png')" width="10" height="10" top="0" right="-15" visible="false" backgroundColor.hovered="#CCCCCC" />
</s:ItemRenderer>
