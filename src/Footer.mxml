<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%">
	
	<fx:Script>
		<![CDATA[
			import components.FooterSceneContainer;
			
			import mx.collections.ArrayCollection;
			
			/**
			 *
			 * Method to create the footer navigation bar.
			 * 
			 **/
			public function buildTrajectory(menu:ArrayCollection, selectedScene:Object, menuItemWidth:Number):void {
				//clean the group before building
				trajMenu.removeAllElements();
				
				var sceneContainer:FooterSceneContainer;
				//for each scene, add a scene object (scene button + tasks checkboxes)
				for each (var scene:Object in menu) {
					sceneContainer = new FooterSceneContainer();
					sceneContainer.width = menuItemWidth-10;
					trajMenu.addElement(sceneContainer);
					sceneContainer.initScene(scene, completedTasks);
				}
				
				updateSelectedFooterScene(selectedScene);				
			}
			
			/**
			 *
			 * Method to update the status of each scene button.
			 * 
			 **/
			public function updateSelectedFooterScene(selectedScene:Object):void {
				var sceneC:FooterSceneContainer;
				for (var i:int = 0; i < trajMenu.numElements; i++) {
					sceneC = trajMenu.getElementAt(i) as FooterSceneContainer;
					sceneC.sceneIndex = i;
					
					//highlight the selected scene
					if(sceneC.sceneId == selectedScene.sceneId) {
						sceneC.currentState = "selected";
					}
					else {
						sceneC.currentState = "default";
					}
				}
			}
			
			/**
			 *
			 * Method to update the status of all tasks.
			 * 
			 **/
			public function updateCompletedTasks(state:String):void {
				if(state == "loggedIn") {
					trajMenu.visible = true;
					var sceneC:FooterSceneContainer;
					for (var i:int = 0; i < trajMenu.numElements; i++) {
						sceneC = trajMenu.getElementAt(i) as FooterSceneContainer;
						sceneC.updateCompletedTasks(completedTasks);
					}
				}
				else {
					trajMenu.visible = false;
				}
				
			}
			
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<fx:Array id="completedTasks" />
	</fx:Declarations>
	
	<s:SkinnableContainer id="footerBar" width="100%" height="24" bottom="0" backgroundColor="#333333" skinClass="skins.footerSkin" />
	
	<s:HGroup id="trajMenu" width="1000" bottom="13" gap="10" horizontalCenter="0"/>
</s:Group>
